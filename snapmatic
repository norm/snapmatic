#!/usr/bin/env perl

use Modern::Perl '2012';
use File::Basename;
use File::Path;
use HTTP::CookieJar;
use HTTP::Tiny;
use IO::All;
use JSON;
use Pod::Usage;

use constant IMG_HOST => 'prod.cloud.rockstargames.com';
use constant WEB_HOST => 'socialclub.rockstargames.com';

my %option;
getopts( 'ho:', \%option );
pod2usage() if $option{'h'};

my $member   = shift // 'invadernorm';
my $platform = shift // 1;
my $photos   = sprintf 'http://%s/member/%s/games/gtav/snapmatic/?platFormId=%d',
                    WEB_HOST, $member, $platform;

# yes, we're over 18
my $jar = HTTP::CookieJar->new();
$jar->add( 'http://socialclub.rockstargames.com', 'UAGC=1' );
$jar->add( 'http://socialclub.rockstargames.com', 'UAGD=10/20/1930' );

# fetch the most recent photos
my $http = HTTP::Tiny->new( cookie_jar => $jar );
my $page = get_content( $http, $photos );

$page =~ m{ modelJson: (.*), \s* $}mx;
my $data = from_json $1;

foreach my $photo ( @$data ) {
    my $id  = $photo->{'Meta'}{'Id'};
    my $src = $photo->{'Meta'}{'ImgSrc'};

    my $filename = "${id}.jpg";

    $filename = "$option{'o'}/$filename"
        if $option{'o'};

    mkpath dirname $filename;

    if ( !-f $filename ) {
        my $img_url = sprintf 'http://%s%s', IMG_HOST, $src;
        my $image   = get_content( $http, $img_url );

        $image > io $filename;
    }
}
exit;

sub get_content {
    my $http = shift;
    my $url  = shift;

    my $get = $http->get( $url );

    die( sprintf '%d %s', $get->{'status'}, $get->{'reason'} )
        unless $get->{'status'} == 200;

    return $get->{'content'};
}

__END__

=head1 SYNOPSIS

B<snapmatic> - fetch photos from Rockstar's Social Club

=head1 USAGE

snapmatic [-o <dir>] <account> [<platform>]

=over

=item -o <dir>

Download files to <dir> rather than current directory

=item <account>

The slug of the Social Club account, normally the account name but lowercase.
You can confirm this by logging into Social Club and clicking the profile link
in the top right. The URL will end with the slug.

=item <platform>

The platform ID: 1 for Xbox 360 (default), 2 for PS3.

=back

=head1 AUTHOR

Mark Norman Francis, <norm@cackhanded.net>.

=head1 COPYRIGHT AND LICENSE

Copyright 2014 Mark Norman Francis.

This library is free software; you can redistribute it and/or modify it
under the same terms as Perl itself.
