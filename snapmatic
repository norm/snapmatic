#!/usr/bin/env perl

use Modern::Perl '2012';
use File::Basename;
use File::Path;
use Getopt::Std;
use HTTP::CookieJar;
use HTTP::Tiny;
use IO::All;
use JSON;
use Pod::Usage;
use POSIX       qw( strftime );

use constant IMG_HOST => 'prod.cloud.rockstargames.com';
use constant WEB_HOST => 'socialclub.rockstargames.com';

my %option;
getopts( 'dhmo:t', \%option );
pod2usage() if $option{'h'};

my $member   = shift // 'invadernorm';
my $platform = shift // 1;
my $photos   = sprintf 'http://%s/member/%s/games/gtav/snapmatic/?platFormId=%d',
                    WEB_HOST, $member, $platform;

# yes, we're over 18
my $jar = HTTP::CookieJar->new();
$jar->add( 'http://socialclub.rockstargames.com', 'UAGC=1' );
$jar->add( 'http://socialclub.rockstargames.com', 'UAGD=10/20/1930' );

# fetch the most recent photos
my $http = HTTP::Tiny->new( cookie_jar => $jar );
my $page = get_content( $http, $photos );

$page =~ m{ modelJson: (.*), \s* $}mx;
my $data = from_json $1;

foreach my $photo ( @$data ) {
    my $id  = $photo->{'Meta'}{'Id'};
    my $src = $photo->{'Meta'}{'ImgSrc'};

    my $taken  = $photo->{'Meta'}{'CreatedTimestamp'};
    my $stamp  = strftime '%Y-%m-%d-%H.%M.%S', localtime($taken);
    my $subdir = strftime '%Y/%m/%d/%H.%M.%S', localtime($taken);

    # save photo file
    my $filename = "${id}.jpg";

    $filename = "${stamp}.jpg"
        if $option{'t'};
    $filename = "${subdir}.jpg"
        if $option{'d'};

    $filename = "$option{'o'}/$filename"
        if $option{'o'};

    mkpath dirname $filename;

    if ( !-f $filename ) {
        my $img_url = sprintf 'http://%s%s', IMG_HOST, $src;
        my $image   = get_content( $http, $img_url );

        $image > io $filename;
    }

    # optionally save json data
    if ( $option{'m'} ) {
        $filename =~ s{\.jpg$}{.json};

        to_json($photo) > io $filename
            if !-f $filename;
    }
}
exit;

sub get_content {
    my $http = shift;
    my $url  = shift;

    my $get = $http->get( $url );

    die( sprintf '%d %s', $get->{'status'}, $get->{'reason'} )
        unless $get->{'status'} == 200;

    return $get->{'content'};
}

__END__

=head1 SYNOPSIS

B<snapmatic> - fetch photos from Rockstar's Social Club

=head1 USAGE

snapmatic [-m] [-o <dir>] [-d|-t] <account> [<platform>]

=over

=item -m

Download the metadata (in JSON format) in addition to photo

=item -o <dir>

Download files to <dir> rather than current directory

=item -t

Use the time the photo was taken as the filename, not the ID. 
eg C<2014-08-11-22.00.42.jpg>

=item -d

Use the time the photo was taken as the filename, not the ID.
Creates subdirectories based on year, month, and day.
eg C<2014/08/11/22.00.42.jpg>.

If both C<-t> and C<-d> are specified, C<-d> takes precedence.

=item <account>

The slug of the Social Club account, normally the account name but lowercase.
You can confirm this by logging into Social Club and clicking the profile link
in the top right. The URL will end with the slug.

=item <platform>

The platform ID: 1 for Xbox 360 (default), 2 for PS3.

=back

=head1 AUTHOR

Mark Norman Francis, <norm@cackhanded.net>.

=head1 COPYRIGHT AND LICENSE

Copyright 2014 Mark Norman Francis.

This library is free software; you can redistribute it and/or modify it
under the same terms as Perl itself.
